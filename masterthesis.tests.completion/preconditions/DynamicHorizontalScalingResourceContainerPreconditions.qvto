import org.scaledl.architecturaltemplates.repositories.cloudscale.black.ProfilesLibrary;

modeltype PCM_ALLOC uses 'http://palladiosimulator.org/PalladioComponentModel/Allocation/5.1';
modeltype PCM_SYS uses 'http://palladiosimulator.org/PalladioComponentModel/System/5.1';
modeltype PCMComposition uses pcm::core::composition('http://palladiosimulator.org/PalladioComponentModel/5.1');
modeltype PCM_RES_ENV uses 'http://palladiosimulator.org/PalladioComponentModel/ResourceEnvironment/5.1';
modeltype ECORE uses 'http://www.eclipse.org/emf/2002/Ecore';

transformation DynamicHorizontalScalingResourceContainerPreconditions(in allocationBeforeCompletion : PCM_ALLOC);

property oldAllocation : Allocation;

main() {
	log('------------Second Iteration------------');

	log('DynamicHorizontalScalingResourceContainer Test started');

	oldAllocation := allocationBeforeCompletion.rootObjects()![Allocation];
	var system : System := oldAllocation.system_Allocation;
	var resourceEnvironment : ResourceEnvironment := oldAllocation.targetResourceEnvironment_Allocation;
	var assemblyContexts : Set(AssemblyContext) := system.assemblyContexts__ComposedStructure;
	
	testSystemProfileApplied(system);
	testResourceEnvironmentProfileApplied(resourceEnvironment);
	testSystemIsDynamicHorizontalScalingResourceContainerSystem(system);
	
	testSystemHasOnlyOneReplicableResourceContainer(resourceEnvironment);
	testNumberOfReplicasGreaterZero(resourceEnvironment);
	testScaleInThresholdGreaterZero(resourceEnvironment);
	testScaleOutThresholdGreaterZeo(resourceEnvironment);
	testReplicableResourceContainerBasicComponentAreStateless(oldAllocation);
	testScaleInSmallerScaleOut(resourceEnvironment);
	
	log('DynamicHorizontalScalingResourceContainer Test finished');
}

query testSystemProfileApplied(system : System){
	assert fatal(isProfileApplied(system.oclAsType(EObject),"DynamicHorizontalScalingResourceContainerProfile"))
		with log ("The System has no DynamicHorizontalScalingResourceContainerProfile applied.");		
}

query testResourceEnvironmentProfileApplied(resourceEnvironment : ResourceEnvironment){
	assert fatal(isProfileApplied(resourceEnvironment.oclAsType(EObject),"DynamicHorizontalScalingResourceContainerProfile"))
		with log ("The resource environment has no DynamicHorizontalScalingResourceContainerProfile applied.");
}

query testSystemIsDynamicHorizontalScalingResourceContainerSystem(system : System){
	assert fatal(hasAppliedStereotype(system,"DynamicHorizontalScalingResourceContainerSystem")) 
		with log('The System is not a DynamicHorizontalScalingResourceContainerSystem.');
}

query testSystemHasOnlyOneReplicableResourceContainer(resourceEnvironment : ResourceEnvironment){
	var loadBalancedResContainers := resourceEnvironment.resourceContainer_ResourceEnvironment->select(r|hasAppliedStereotype(r,'ReplicableResourceContainer'));
	var loadBalancedResContainersSize : Integer := loadBalancedResContainers->size();
	assert error(loadBalancedResContainersSize = 1) 
		with log('There is not exactly one Replicable Resource Container. Instead there are ' + loadBalancedResContainersSize.toString() + '.');
}

query testNumberOfReplicasGreaterZero(resourceEnvironment : ResourceEnvironment){
	var replicableResContainer := resourceEnvironment.resourceContainer_ResourceEnvironment->selectOne(r|hasAppliedStereotype(r,'ReplicableResourceContainer'));
	if(replicableResContainer != null){
		var numberOfReplicas : Integer := getIntTaggedValue(replicableResContainer, "numberOfReplicas", "ReplicableResourceContainer");
		assert error(numberOfReplicas>0) 
			with log('Number of Replicas is not greater 0.');
	}else{
		assert error(replicableResContainer != null) 
			with log('There is no replicable Resource Container, hence the number of replicas cannot be tested.');
	};
}

query testScaleInThresholdGreaterZero(resourceEnvironment : ResourceEnvironment){
	var replicableResContainer := resourceEnvironment.resourceContainer_ResourceEnvironment->selectOne(r|hasAppliedStereotype(r,'ReplicableResourceContainer'));
	if(replicableResContainer != null){
		var scaleInThreshold : Real := getDoubleTaggedValue(replicableResContainer, "scaleInThreshold", "ReplicableResourceContainer");
		assert error(scaleInThreshold>0) 
			with log('Scale In Threshold is not greater 0.');
	}else{
		assert error(replicableResContainer != null) 
			with log('There is no replicable Resource Container, hence the scale in threshold cannot be tested.');
	};
}

query testScaleOutThresholdGreaterZeo(resourceEnvironment : ResourceEnvironment){
	var replicableResContainer := resourceEnvironment.resourceContainer_ResourceEnvironment->selectOne(r|hasAppliedStereotype(r,'ReplicableResourceContainer'));
	
	if(replicableResContainer != null){
		var scaleOutThreshold : Real := getDoubleTaggedValue(replicableResContainer, "scaleOutThreshold", "ReplicableResourceContainer");
		assert error(scaleOutThreshold>0) 
			with log('Scale Out Threshold is not greater 0.');
	}else{
		assert error(replicableResContainer != null) 
			with log('There is no replicable Resource Container, hence the scale out threshold cannot be tested.');
	};
}

query testReplicableResourceContainerBasicComponentAreStateless(allocation : Allocation){
	var allStateless : Boolean := allocation.allocationContexts_Allocation->select(ac|
									hasAppliedStereotype(ac.resourceContainer_AllocationContext,'ReplicableResourceContainer'))->collect(ac|
									ac.assemblyContext_AllocationContext.encapsulatedComponent__AssemblyContext)->forAll(bc|
									hasAppliedStereotype(bc,'StatelessComponent'));
	assert error(allStateless) 
		with log('Not all Basic Components, which are allocated to the Replicable Resource Container, are stateless.');
}

query testScaleInSmallerScaleOut(resourceEnvironment : ResourceEnvironment){
	var replicableResourceContainer : ResourceContainer := resourceEnvironment.resourceContainer_ResourceEnvironment->selectOne(resCon|hasAppliedStereotype(resCon,'ReplicableResourceContainer'));
	if(replicableResourceContainer != null){
		assert error(getDoubleTaggedValue(replicableResourceContainer,'scaleInThreshold','ReplicableResourceContainer')<getDoubleTaggedValue(replicableResourceContainer,'scaleOutThreshold','ReplicableResourceContainer')) 
			with log('Replicable Resource Container Scale In Threshold is greater or equal than the Scale Out Threshold.');
	}
	else {assert error(false) 
			with log('There is no Replicable ResourceContainer, hence scale in greater than scale out cannot be tested.');
	}
}